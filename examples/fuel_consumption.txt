<<dd_version: 1>>

<head>
<link rel="stylesheet" type="text/css" href="stmarkdown.css">
<script type="text/x-mathjax-config">
  MathJax.Hub.Config({tex2jax: {inlineMath: [['$','$'], ['\\(','\\)']]}});
</script>
<script type="text/javascript" async
  src="https://cdn.mathjax.org/mathjax/latest/MathJax.js?config=TeX-AMS_CHTML">
</script>
</head>

<<dd_do: quietly>>
syntax [anything(name=detail)]
<</dd_do>>

<<dd_do: quietly>>
set linesize 80
<</dd_do>>


# Stata车辆数据文件中车型的重量和油耗之间关系的对比和分析

~~~~
<<dd_do>>
use auto_zh, clear
<</dd_do>>
~~~~

## 检查数据

~~~~
<<dd_do>>
describe 油耗 重量
<</dd_do>>
~~~~

<<dd_do:quietly>>
assert 油耗 > 0 
<</dd_do>>

~~~~
<<dd_do>>
summarize 重量
<</dd_do>>
~~~~

变量重量的最小值<<dd_display: %4.2f `r(min)'>>,最大值<<dd_display: %4.2f `r(max)'>>,极差<<dd_display: %4.2f `r(max)'-`r(min)'>>。

##  用散点图显示油耗与重量关系 

~~~~
<<dd_do>>
scatter 油耗 重量, mcolor(blue%50)
<</dd_do>>
~~~~

<<dd_graph: sav("sc_gp100m_mpg.png") alt("油耗重量散点图") replace markdown>>

## 用线性回归研究油耗与重量关系

~~~~
<<dd_do>>
regress 油耗 重量
<</dd_do>>
~~~~

<<dd_do:quietly>>
matrix define eb = e(b)
<</dd_do>>

线性回归结果显示重量每增加一百公斤,每百公里油耗增加<<dd_display:%9.4f eb[1,1]*100>>公升。 

## 用线性回归结果生成HTML表格

<<dd_do>>
_coef_table, markdown
<</dd_do>>

## 用**estimates table**生成表格

~~~~
<<dd_do>>
quietly regress 油耗 重量 变速比 转弯半径
estimates store 模型1
quietly regress 油耗 重量 变速比 转弯半径 国籍
estimates store  模型2
estimates table 模型1 模型2, b(%7.4f) stats(N r2_a) star
<</dd_do>>
~~~~

<<dd_do>>
estimates table 模型1 模型2, varlabel b(%7.4f) stats(N r2_a) star markdown
<</dd_do>>

## 用**esttab**生成表格

<<dd_do: quietly>>
cap erase esttab_ex.html
eststo clear
<</dd_do>>

~~~~
<<dd_do>>
eststo : quietly regress 油耗 重量 变速比 转弯半径
eststo : quietly regress 油耗 重量 变速比 转弯半径 国籍
esttab using esttab_ex.html, label   /// 
	width(80%) nogaps                ///
	mtitles("模型1" "模型2")       ///
	title(线性回归结果) 
<</dd_do>>
~~~~~

<<dd_include:esttab_ex.html>>

<<dd_do: quietly>>
cap erase esttab_ex.html
<</dd_do>>

The community-contributed **esttab** is available on the Boston College Statistical 
Software Components (SSC) archive; see [ssc install](https://www.stata.com/support/ssc-installation/) 
for details.

