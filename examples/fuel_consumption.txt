<<dd_version: 1>>

<head>
<meta charset="UTF-8">
<link rel="stylesheet" type="text/css" href="stmarkdown.css">
<script type="text/x-mathjax-config">
  MathJax.Hub.Config({tex2jax: {inlineMath: [['$','$'], ['\\(','\\)']]}});
</script>
<script type="text/javascript" async
  src="https://cdn.mathjax.org/mathjax/latest/MathJax.js?config=TeX-AMS_CHTML">
</script>
</head>

<<dd_do: quietly>>
syntax [anything(name=detail)]
<</dd_do>>

<<dd_do: quietly>>
set linesize 80
<</dd_do>>


# Stata车辆数据文件中车型的重量和油耗之间关系的对比和分析

我们希望研究1978车辆数据中两个变量**油耗**和**重量**之间的关系。

~~~~
<<dd_do:nooutput>>
use auto_zh, clear
<</dd_do>>
~~~~

## 检查数据

首先我们检查**油耗**和**重量**的变量描述和摘要统计数据。

~~~~
<<dd_do>>
describe 油耗 重量
<</dd_do>>
~~~~

<<dd_do:quietly>>
assert 油耗 > 0 
<</dd_do>>

~~~~
<<dd_do>>
summarize 油耗
<</dd_do>>
~~~~

从摘要统计数据看出，变量**油耗**的最小值<<dd_display: %4.2f `r(min)'>>,最大值<<dd_display: %4.2f `r(max)'>>,极差<<dd_display: %4.2f `r(max)'-`r(min)'>>。

~~~~
<<dd_do>>
summarize 重量
<</dd_do>>
~~~~

从摘要统计数据看出，变量**重量**的最小值<<dd_display: %4.2f `r(min)'>>,最大值<<dd_display: %4.2f `r(max)'>>,极差<<dd_display: %4.2f `r(max)'-`r(min)'>>。


##  用散点图显示油耗与重量关系 

~~~~
<<dd_do>>
twoway lfitci 油耗 重量 || scatter 油耗 重量, mcolor(%20) scheme(538)
<</dd_do>>
~~~~

<<dd_graph: sav("sc_gp100m_mpg.png") alt("油耗重量散点图") replace markdown>>

我们在**油耗**和**重量**的散点图上叠加拟合值与均值的置信区间。

## 用线性回归研究油耗与重量关系

~~~~
<<dd_do>>
regress 油耗 重量
<</dd_do>>
~~~~

<<dd_do:quietly>>
matrix define eb = e(b)
<</dd_do>>

线性回归结果显示**重量**每增加一百公斤,**每百公里油耗**增加<<dd_display:%9.4f eb[1,1]*100>>公升, 
可由模型解释的观察到的方差量为<<dd_display:%9.0f e(r2)*100>>%. 

## 用线性回归结果生成HTML表格

<<dd_do>>
_coef_table, markdown
<</dd_do>>

## 用**estimates table**生成表格

~~~~
quietly regress 油耗 重量 变速比 转弯半径
estimates store 模型1
quietly regress 油耗 重量 变速比 转弯半径 国籍
estimates store  模型2
estimates table 模型1 模型2, b(%7.4f) stats(N r2_a) star
~~~~

<<dd_do: quietly>>
quietly regress 油耗 重量 变速比 转弯半径
estimates store 模型1
quietly regress 油耗 重量 变速比 转弯半径 国籍
estimates store  模型2
estimates table 模型1 模型2, b(%7.4f) stats(N r2_a) star
<</dd_do>>

<<dd_do>>
estimates table 模型1 模型2, varlabel b(%7.4f) stats(N r2_a) star markdown
<</dd_do>>

## 用**esttab**生成表格

<<dd_do: quietly>>
cap erase esttab_ex.html
eststo clear
<</dd_do>>

~~~~~
eststo : quietly regress 油耗 重量 变速比 转弯半径
eststo : quietly regress 油耗 重量 变速比 转弯半径 国籍
esttab using esttab_ex.html, label   /// 
	width(80%) nogaps                ///
	mtitles("模型1" "模型2")       ///
	title(线性回归结果) 
~~~~~

<<dd_do: quietly>>
eststo : quietly regress 油耗 重量 变速比 转弯半径
eststo : quietly regress 油耗 重量 变速比 转弯半径 国籍
esttab using esttab_ex.html, label   /// 
	width(80%) nogaps                ///
	mtitles("模型1" "模型2")       ///
	title(线性回归结果) 
<</dd_do>>

<<dd_include:esttab_ex.html>>

<<dd_do: quietly>>
cap erase esttab_ex.html
<</dd_do>>

The community-contributed **esttab** is available on the Boston College Statistical 
Software Components (SSC) archive; see [ssc install](https://www.stata.com/support/ssc-installation/) 
for details.

