<<dd_version: 1>>

<head>
<link rel="stylesheet" type="text/css" href="stmarkdown.css">
<script type="text/x-mathjax-config">
  MathJax.Hub.Config({tex2jax: {inlineMath: [['$','$'], ['\\(','\\)']]}});
</script>
<script type="text/javascript" async
  src="https://cdn.mathjax.org/mathjax/latest/MathJax.js?config=TeX-AMS_CHTML">
</script>
</head>

<<dd_do:quietly>>
set linesize 80
<</dd_do>>

# A fuel consumption study of Stata's auto dataset

We conduct a study of the fuel consumption of cars in Stata's auto dataset. 

~~~~
<<dd_do>>
sysuse auto, clear
<</dd_do>>
~~~~

## Perform data transformation

We generate a variable, **fuel**, that measures the fuel consumption rate in the unit of 
Gallons per 100 Miles.  

~~~~
<<dd_do>>
generate fuel = 100/mpg
label variable fuel "Fuel consumption (Gallons per 100 Miles)"
<</dd_do>>
~~~~

## Examine the variables

We examine variables for possible errors in the data.

~~~~
<<dd_do>>
describe fuel weight
<</dd_do>>
~~~~

<<dd_do:quietly>>
assert fuel > 0 
<</dd_do>>

~~~~
<<dd_do>>
summarize weight
<</dd_do>>
~~~~

The variable **weight** has minimum value <<dd_display: %4.2f `r(min)'>>, maximum 
value <<dd_display: %4.2f `r(max)'>>, and range <<dd_display: %4.2f `r(max)'-`r(min)'>>.

##  Plot fuel consumption and vehicle weight 

~~~~
<<dd_do>>
scatter fuel weight, mcolor(blue%50)
<</dd_do>>
~~~~

<<dd_graph: saving("sc_fuel_cc.png") alt("scatter fuel weight") replace markdown>>

## Explore relationship between fuel consumption and vehicle weight - linear regression

~~~~
<<dd_do>>
regress fuel weight
<</dd_do>>
~~~~

<<dd_do:quietly>>
matrix define eb = e(b)
<</dd_do>>

<<dd_skip_if: (eb[1,1] < 0)>>
The regression shows that for every unit increase in weight, a 
<<dd_display:%9.4f eb[1,1]>> unit increase in fuel consumption is predicted.
<<dd_skip_else>>
The regression shows that for every unit increase in weight, a 
<<dd_display:%9.4f -eb[1,1]>> unit decrease in fuel consumption is predicted. 
<<dd_skip_end>>

